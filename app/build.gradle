apply plugin: 'com.android.application'
//apply plugin: 'jacoco-android'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --abbrev=0 --tags'
        .execute([], project.rootDir).text.trim().replaceAll("v", "")
def gitTagCount = Integer.parseInt('git rev-list --tags --no-walk --count'
        .execute([], project.rootDir).text.trim())

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "org.xdty.callerinfo"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode gitTagCount
        versionName gitTag

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""

        setProperty("archivesBaseName", "CallerInfo-v$versionName")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile('proguard-test-rules.pro')
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    dexOptions {
        javaMaxHeapSize "2g"
        preDexLibraries true
    }
}

configurations {
//    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"

    androidTestImplementation "androidx.test.ext:junit:$extJunit_version"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:$uiautomator_version"
    androidTestImplementation "androidx.test:rules:$testRules_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$espresso_version"
    androidTestImplementation("androidx.test.espresso:espresso-contrib:$espresso_version") {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    }
    androidTestImplementation "org.hamcrest:hamcrest-integration:$hamcrest_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko:$anko_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "androidx.core:core-ktx:$ktx_version"

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.annotation:annotation:$annotation_version"
    implementation "androidx.cardview:cardview:$cardview_version"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.browser:browser:$browser_version"
    implementation "android.arch.work:work-runtime:$work_version"

    implementation "com.google.guava:guava:$guava_version"
    implementation "com.google.android.material:material:$material_version"

    implementation "org.xdty.preference:color-picker:$color_picker_version"
    implementation "com.minimize.library:seekbar-compat:$seek_bar_version"
    implementation "cat.ereza:customactivityoncrash:$crash_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // crash report
    implementation "com.google.firebase:firebase-core:$firebase_version"
    implementation "com.tencent.bugly:crashreport:$bugly_version"

    implementation "com.pkmmte.view:circularimageview:$circular_version"
    implementation "com.afollestad.material-dialogs:core:$material_dialog_version"
    implementation "org.apmem.tools:layouts:$flow_layout_version@aar"

    // requerydb
    implementation "io.requery:requery:$requery_version"
    implementation "io.requery:requery-android:$requery_version"
    kapt "io.requery:requery-processor:$requery_version"

    implementation("net.openid:appauth:$auth_version") {
        exclude group: 'com.android.support', module: 'customtabs'
    }

    debugImplementation "com.facebook.stetho:stetho:$stetho_version"
    debugImplementation "com.amitshekhar.android:debug-db:$debug_db_version"

    implementation project(':phone-number')
    implementation project(':config')
    implementation project(':standOut')
}

apply from: '../signing.gradle'
apply from: '../manifest.gradle'

apply plugin: 'com.google.gms.google-services'
